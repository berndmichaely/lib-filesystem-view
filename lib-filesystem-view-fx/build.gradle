plugins {
	id 'java-library'
	id 'org.openjfx.javafxplugin' version '0.1.0'
	id 'jacoco'
	id 'maven-publish'
	id 'org.checkerframework' version '0.6.60' apply false
}

apply plugin: 'org.checkerframework'

wrapper {
	gradleVersion '8.7'
}

final def javaLangVersion = JavaLanguageVersion.of(17)
final def jvmVendor = JvmVendorSpec.ADOPTIUM

java {
	toolchain {
		languageVersion = javaLangVersion
		vendor = jvmVendor
	}
	withJavadocJar()
	withSourcesJar()
}
tasks.withType(JavaExec).configureEach {
	javaLauncher = javaToolchains.launcherFor {
		languageVersion = javaLangVersion
		vendor = jvmVendor
	}
}

group = 'de.bernd_michaely'
version = '0.9.5'
//version = '0.9.5-snapshot'

final def regexSemVer = ~/^(0|[1-9]\d*)\.(0|[1-9]\d*)\.(0|[1-9]\d*)(?:-((?:0|[1-9]\d*|\d*[a-zA-Z-][0-9a-zA-Z-]*)(?:\.(?:0|[1-9]\d*|\d*[a-zA-Z-][0-9a-zA-Z-]*))*))?(?:\+([0-9a-zA-Z-]+(?:\.[0-9a-zA-Z-]+)*))?$/
if (!(version =~ regexSemVer)) throw new StopExecutionException("Invalid semantic version »${version}«")

ext.versionJUnit = '5.13.4'
ext.versionCheckerFramework = '3.49.4'

jacoco {
	toolVersion = '0.8.10'
}

final def deactivateCheckerFramework = false

javafx {
	version = "21.0.8"
	modules = [ 'javafx.controls' ]
	configurations = [ 'implementation', 'testImplementation']
}

logger.lifecycle "Running Gradle $project.gradle.gradleVersion"
logger.lifecycle "Java source level           : ${java.sourceCompatibility}"
logger.lifecycle "Compile      -> Debug-Level : ${compileJava.options.debug}"
logger.lifecycle "Test-Compile -> Debug-Level : ${compileTestJava.options.debug}"
logger.lifecycle "Modularity enabled          : ${java.modularity.inferModulePath.get()}"
logger.lifecycle "==> Root project »$name ($version)«"
logger.lifecycle " -> Base dir is »${file('.')}«"
logger.lifecycle " -> Using »JUnit $versionJUnit«"
logger.lifecycle " -> Using »JaCoCo ${jacoco.toolVersion}«"
logger.lifecycle " -> Checker Framework is ${deactivateCheckerFramework ? 'DEACTIVATED!' : 'active.'}"
logger.lifecycle " -> Using »JavaFX ${javafx.version}«"
logger.lifecycle " -> Included child projects : »${childProjects.keySet()}«"
logger.lifecycle " -> Maven coordinates »${group}:${name}:${version}«"

repositories {
  mavenCentral()
}

dependencies {
	implementation "de.bernd_michaely:common-filesystem-view-base:${version}"
}
	
tasks.named('test') {
	logger.lifecycle "=> Configuring task »$name« for JUnit"
	systemProperty 'java.util.logging.config.file', file('src/test/java/logging.properties')
	// Use JUnit Platform for unit tests.
	useJUnitPlatform()
	logger.lifecycle "   -> JVM args : $jvmArgs"
}

testing {
	suites {
		test {
			useJUnitJupiter(versionJUnit)
		}
	}
}

task runDemo {
	group = 'Demo'
	description 'Run a demo (runs the unit test in a demo mode showing the application window)'
	final boolean isRunningDemo = project.gradle.startParameter.taskNames.contains(name)
	final String modeTestDemo = isRunningDemo ? 'demo' : 'test'
	logger.lifecycle "   -> Test/Demo Mode : »${modeTestDemo}«"
	test.systemProperty '_custom.test.mode', "${modeTestDemo}"
	dependsOn clean
	dependsOn test
}

jacocoTestReport {
	dependsOn test
	doLast {
		final File jacocoReportsDir = reports.html.outputLocation.asFile.get()
		final URL jacocoReportsUrl = new URL("file", null, "${jacocoReportsDir}/index.html")
		logger.lifecycle " -> JaCoCo reports in »${jacocoReportsUrl}«"
	}
}

checkerFramework {
  checkers += 'org.checkerframework.checker.nullness.NullnessChecker'
  skipCheckerFramework = deactivateCheckerFramework
	excludeTests = true
}

tasks.withType(JavaCompile) {
	logger.lifecycle "=> Configuring task »$name«"
	options.compilerArgs += '-Xlint:unchecked'
	options.compilerArgs += '-Xlint:deprecation'
	logger.lifecycle "   -> compiler args : ${options.compilerArgs}"
}

javadoc {
	def headerPrefix = '<div style="margin-top:14px;"><strong>'
	def headerPostfix = '</strong></div>'
	def header = headerPrefix + project.name + " (" + project.version + ')' + headerPostfix
	def javafxVer
	final def m = javafx.version =~ /(\d+)(\.\d+(\.\d+)?)?(-.+)?(\+.+)?/
	if (m) {
		javafxVer = m.group(1)
		logger.lifecycle " -> JavaFX major version is ${javafxVer}"
	} else {
		throw new StopExecutionException("Can't get JavaFX major version.")
	}
	options.header header
	options.links "https://docs.oracle.com/en/java/javase/${java.sourceCompatibility}/docs/api"
	options.links "https://openjfx.io/javadoc/${javafxVer}/"
	options.links 'https://checkerframework.org/api'
	options.windowTitle project.version
	options.use true
}

task checkModuleNamingConvention (type: org.gradle.CheckModuleNamingConvention) {}
//check.dependsOn checkModuleNamingConvention
clean.dependsOn checkModuleNamingConvention

publishing {
	publications {
		libCommonFilesystemViewFX(MavenPublication) {
			from components.java
      pom {
        name = 'common-filesystem-view-fx'
        description = 'JavaFX implementation for module common-filesystem-view-base'
        url = 'https://bernd-michaely.de'
        licenses {
          license {
            name = 'Apache License, Version 2.0'
            url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }
      }
		}
	}
}
